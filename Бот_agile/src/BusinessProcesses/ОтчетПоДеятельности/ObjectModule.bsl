

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ДействиеЗаСколькоДнейПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	
	СтруктураКлавиатуры = Новый Структура;
	МассивКнопок = Новый Массив;
	
	СоответствиеКоличестваДнейИИдентификаторов = СоответствиеКоличестваДнейИИдентификаторов();
	
	Для Каждого КлючИЗначение Из СоответствиеКоличестваДнейИИдентификаторов Цикл
		ИнлайнКнопка = ИнтеграцияСМессенджерами.ИнлайнКнопка(КлючИЗначение.Значение, КлючИЗначение.Ключ);
		МассивКнопок.Добавить(ИнлайнКнопка);
	КонецЦикла;	
	
	СтруктураКлавиатуры.Вставить("inline_keyboard", МассивКнопок);
	
	СтрJSON = КоннекторHTTP.ОбъектВJson(СтруктураКлавиатуры);
	
	ДопПараметры = "&parse_mode=HTML&reply_markup=" + СтрJSON;
	
	ИнтеграцияСМессенджерами.TelegramSendMessage(Бот.Токен, "За сколько дней?", Пользователь.Chat_id, ДопПараметры);

	
КонецПроцедуры

Процедура ЗавершениеПриЗавершении(ТочкаМаршрутаБизнесПроцесса, Отказ)
	
	Если ЗначениеЗаполнено(ЧислоДнейОтчета) Тогда
		ОтправитьОтчетПоДеятельности();
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоответствиеКоличестваДнейИИдентификаторов() 
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить("ОтчетПоДеятельности_7"  , "7");
	Соответствие.Вставить("ОтчетПоДеятельности_14" , "14");
	Соответствие.Вставить("ОтчетПоДеятельности_30" , "30");
	Соответствие.Вставить("ОтчетПоДеятельности_365", "365");
	
	Возврат Соответствие;
	
КонецФункции	

Процедура ОтправитьОтчетПоДеятельности()
	
	Перем Выборкаchat_id, ВыборкаДетальныеЗаписи, Запрос, РезультатЗапроса, СтрокаСообщения;
	
	//надо где-то хранить руководителя
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатусМитинг.Пользователь КАК Пользователь,
	|	ЗадачиПользователей.Наименование КАК Вопрос,
	|	ЗадачиПользователей.Ответ КАК Ответ,
	|	СтатусМитинг.Дата КАК Дата
	|ИЗ
	|	БизнесПроцесс.СтатусМитинг КАК СтатусМитинг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Задача.ЗадачиПользователей КАК ЗадачиПользователей
	|		ПО СтатусМитинг.Ссылка = ЗадачиПользователей.БизнесПроцесс
	|ГДЕ
	|	СтатусМитинг.Дата >= ДОБАВИТЬКДАТЕ(&НачалоДня, ДЕНЬ, &ЧислоДней * -1)
	|	И НЕ СтатусМитинг.ПометкаУдаления
	|	И СтатусМитинг.Пользователь = &Пользователь
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|ИТОГИ ПО
	|	Дата";
	
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ЧислоДней", ЧислоДнейОтчета);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтрокаСообщения = "";
	
	ВыборкаДата = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДата.Следующий() Цикл
		
		СтрокаСообщения = СтрокаСообщения+Символы.ПС+Формат(ВыборкаДата.Дата, "ДФ=dd.MM"); 
		
		ВыборкаДетальныеЗаписи = ВыборкаДата.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ТекущееДействие = СтрШаблон("%1 %2", ВыборкаДетальныеЗаписи.Вопрос, ВыборкаДетальныеЗаписи.Ответ);
			СтрокаСообщения = СтрокаСообщения+Символы.ПС+ТекущееДействие;
			
		КонецЦикла;

	КонецЦикла;
	
	Если ЗначениеЗаполнено(СтрокаСообщения) Тогда
		СтрокаСообщения = "Лови отчет"+СтрокаСообщения;
		ИнтеграцияСМессенджерами.TelegramSendMessage(Бот.Токен , СтрокаСообщения, Пользователь.chat_id);
	Иначе
		ИнтеграцияСМессенджерами.TelegramSendMessage(Бот.Токен , "Нет данных", Пользователь.chat_id);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействиеЗаСколькоДнейОбработкаПроверкиВыполнения(ТочкаМаршрутаБизнесПроцесса, Задача, Результат)
	// Вставить содержимое обработчика.
КонецПроцедуры

Процедура ДействиеЗаСколькоДнейПриВыполнении(ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)
	
	ИдСообщения = Задача.ИдСообщения;
	Если ЗначениеЗаполнено(ИдСообщения) Тогда
		ИнтеграцияСМессенджерами.УдалитьИнлайнКлавиатуру(Пользователь.chat_id, ИдСообщения, Бот.Токен);
	КонецЕсли;
	
	СоответствиеКоличестваДнейИИдентификаторов = СоответствиеКоличестваДнейИИдентификаторов();
	КоличествоДней = СоответствиеКоличестваДнейИИдентификаторов.Получить(Задача.Ответ);
	
	ОписаниеЧисло = Новый ОписаниеТипов("Число");
	ЧислоДнейОтчета = ОписаниеЧисло.ПривестиЗначение(КоличествоДней);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли